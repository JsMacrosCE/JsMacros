plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

// Configuration for embedding extension jars
configurations {
    extensionJars {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    
    // ModMenu integration
    modImplementation "com.terraformersmc:modmenu:15.0.0"
    
    // Common library dependencies - include for bundling in jar
    implementation include(group: 'io.noties', name: 'prism4j', version: '2.0.0')
    implementation include(group: 'org.jooq', name: 'joor', version: '0.9.15')
    implementation include(group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.14')
    implementation include(group: 'org.javassist', name: 'javassist', version: '3.30.2-GA')
    
    // Extension jars to embed
    extensionJars project(path: ':extension:graal', configuration: 'archives')
    extensionJars project(path: ':extension:graal:js', configuration: 'archives')
}

// Collect extension jar names for dependencies property
def getExtensionJarPaths() {
    return configurations.extensionJars.files.collect { file ->
        "\"META-INF/jsmacrosdeps/${file.name}\""
    }.join(', ')
}

tasks.processResources {
    // Embed extension jars into the final jar
    dependsOn configurations.extensionJars
    from(configurations.extensionJars) {
        into 'META-INF/jsmacrosdeps'
    }

    // Add dependencies expansion for jsmacros.extension.json
    filesMatching('jsmacros.extension.json') {
        expand 'dependencies': getExtensionJarPaths()
    }
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}
