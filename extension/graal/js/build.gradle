plugins {
    id 'java-library'
}

base {
    archivesName = "${mod_id}-graal-js"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
}

repositories {
    mavenCentral()
}

// Configuration for runtime dependencies to embed in the extension jar
configurations {
    embedDeps {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    // Depends on graal module
    implementation project(':extension:graal')
    implementation project(':extension')
    implementation project(':common')
    
    // Graal JS specific dependencies
    implementation group: 'org.graalvm.polyglot', name: 'js', version: '24.0.1'
    
    // Embed GraalJS dependencies
    embedDeps group: 'org.graalvm.truffle', name: 'truffle-enterprise', version: '24.0.1'
    embedDeps group: 'org.graalvm.js', name: 'js-language', version: '24.0.1'
    embedDeps group: 'org.graalvm.truffle', name: 'truffle-runtime', version: '24.0.1'
    embedDeps group: 'org.graalvm.truffle', name: 'truffle-compiler', version: '24.0.1'
    embedDeps group: 'org.graalvm.sdk', name: 'nativebridge', version: '24.0.1'
    embedDeps group: 'org.graalvm.sdk', name: 'jniutils', version: '24.0.1'
    
    // Test dependencies
    testImplementation project(':extension')
    testImplementation project(':extension').sourceSets.test.output
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
    testImplementation group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'
}

// Collect embedded dependency paths for the json file
def getEmbeddedDepPaths() {
    return configurations.embedDeps.files.collect { file ->
        "\"META-INF/jsmacrosdeps/${file.name}\""
    }.join(', ')
}

// Process resources to expand dependencies placeholder
processResources {
    filesMatching('jsmacros.ext.graaljs.json') {
        expand 'dependencies': getEmbeddedDepPaths()
    }
}

// Embed dependencies into the extension jar
jar {
    dependsOn configurations.embedDeps
    from(configurations.embedDeps) {
        into 'META-INF/jsmacrosdeps'
    }
}

tasks.test {
    useJUnitPlatform()
}
